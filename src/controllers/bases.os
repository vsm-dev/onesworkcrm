Функция Index() Экспорт
	СписокИнформационныхБаз = ИнформационныеБазы.ПолучитьСписок();
	Возврат Представление(СписокИнформационныхБаз);
КонецФункции

Функция Add() Экспорт
	Если ЗапросHttp.Метод = "POST" Тогда
		ТЗ = ИнформационныеБазы.ПолучитьСписок();
		Элемент = ТЗ.Добавить();
		ЗаполнитьДанные(Элемент);
		ИнформационныеБазы.Записать(ТЗ);
		Возврат Перенаправление("/bases/index");
	КонецЕсли;

	Возврат Представление("Item");
КонецФункции

Функция Edit() Экспорт
	ИдентификаторБазы = ЗначенияМаршрута["id"];

	Если ИдентификаторБазы = Неопределено Тогда
		Возврат Перенаправление("/bases/index");
	КонецЕсли;

	ТЗ = ИнформационныеБазы.ПолучитьСписок();
	Элемент = ТЗ.Найти(ИдентификаторБазы, "ИдентификаторБазы");

	Если Элемент = Неопределено Тогда
		Возврат КодСостояния(404);
	КонецЕсли;

	Если ЗапросHttp.Метод = "POST" Тогда
		ЗаполнитьДанные(Элемент);
		ИнформационныеБазы.Записать(ТЗ);
		
		Возврат Перенаправление("/bases/index");
	Иначе
		// Передаем в представление "модель" - Элемент
		Возврат Представление("Item", Элемент);
	КонецЕсли;
КонецФункции

Функция Delete() Экспорт
	ИдентификаторБазы = ЗначенияМаршрута["id"];

	Если ИдентификаторБазы = Неопределено Тогда
		Возврат Перенаправление("/bases/index");
	КонецЕсли;

	ТЗ = ИнформационныеБазы.ПолучитьСписок();
	Элемент = ТЗ.Найти(ИдентификаторБазы, "ИдентификаторБазы");

	Если Элемент = Неопределено Тогда
		Возврат КодСостояния(404);
	КонецЕсли;

	ТЗ.Удалить(Элемент);
	ИнформационныеБазы.Записать(ТЗ);
	
	Возврат Перенаправление("/bases/index");
КонецФункции

Процедура ЗаполнитьДанные(Знач Элемент)
	ДанныеФормы = ЗапросHttp.ДанныеФормы;
	Если ЗначениеЗаполнено(ДанныеФормы["ИдентификаторБазы"]) Тогда
		Элемент.ИдентификаторБазы = ДанныеФормы["ИдентификаторБазы"];
	Иначе
		Элемент.ИдентификаторБазы = "id" + Строка(Новый УникальныйИдентификатор());
	КонецЕсли;
	Элемент.Наименование = ДанныеФормы["Наименование"];
	Элемент.ВерсияПлатформы = ДанныеФормы["ВерсияПлатформы"];
	Элемент.ПользовательПоУмолчанию = ДанныеФормы["ПользовательПоУмолчанию"];
	Элемент.ПарольПоУмолчанию = ДанныеФормы["ПарольПоУмолчанию"];
	Элемент.ПутьКФайловойБазе = ДанныеФормы["ПутьКФайловойБазе"];
	Элемент.ИмяСервера = ДанныеФормы["ИмяСервера"];
	Элемент.ИмяСервернойБазы = ДанныеФормы["ИмяСервернойБазы"];
КонецПроцедуры