#Использовать json

Перем мПутьКФайлуНастроек; // путь к файлу с настройками баз

Функция СодержимоеНастроек()
    
    Файл = Новый Файл(мПутьКФайлуНастроек);
    Если Не Файл.Существует() Тогда
        Содержимое = "[]";
    Иначе
        Текст = Новый ЧтениеТекста(мПутьКФайлуНастроек);
        Содержимое = Текст.Прочитать();
        Текст.Закрыть();
    КонецЕсли;
    
    Возврат Содержимое;
    
КонецФункции

Функция ПолучитьСписок() Экспорт

    СписокБаз = Новый ТаблицаЗначений;
    СписокБаз.Колонки.Добавить("ИдентификаторБазы");
    СписокБаз.Колонки.Добавить("Наименование");
    СписокБаз.Колонки.Добавить("ВерсияПлатформы");
    СписокБаз.Колонки.Добавить("ПользовательПоУмолчанию");
	СписокБаз.Колонки.Добавить("ПарольПоУмолчанию");
	СписокБаз.Колонки.Добавить("ПутьКФайловойБазе");
	СписокБаз.Колонки.Добавить("ИмяСервера");
	СписокБаз.Колонки.Добавить("ИмяСервернойБазы");

    Парсер = Новый ПарсерJSON;
    Данные = Парсер.ПрочитатьJSON(СодержимоеНастроек(), , , Истина);
    Если ТипЗнч(Данные) <> Тип("Массив") Тогда
        Возврат СписокБаз;
    КонецЕсли;

    Для каждого ОбъектДанных Из Данные Цикл
        ЗаполнитьЗначенияСвойств(СписокБаз.Добавить(), ОбъектДанных);
    КонецЦикла;

    Возврат СписокБаз;

КонецФункции

Процедура Записать(Знач ТЗ) Экспорт

	Текст = Новый ЗаписьТекста(мПутьКФайлуНастроек);
	Запись = Новый ПарсерJSON;
	Текст.Записать(Запись.ЗаписатьJSON(ТЗ));
	Текст.Закрыть();

КонецПроцедуры

///////////////////////////////////////////////////
мПутьКФайлуНастроек = "appData.json";